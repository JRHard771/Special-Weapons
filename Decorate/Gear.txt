ACTOR FuseTics : Inventory
{
	Inventory.MaxAmount 140
}

ACTOR FragGrenAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Inventory.PickupSound "misc/pkupitem"
	Inventory.PickupMessage "Picked up a frag grenade!"
	Ammo.DropAmount 1
	Ammo.BackPackAmount 1
	Ammo.BackPackMaxAmount 3
	Scale 0.3
	States
	{
	Spawn:
		FGRN ACG 0 NoDelay A_Jump(85,"Idle")
	Idle:
		"####" "#" -1
		stop
	}
}

ACTOR SmokeGrenAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Inventory.PickupSound "misc/pkupitem"
	Inventory.PickupMessage "Picked up a smoke grenade!"
	Ammo.DropAmount 1
	Ammo.BackPackAmount 1
	Ammo.BackPackMaxAmount 3
	Scale 0.3
	States
	{
	Spawn:
		SGRN ACG 0 NoDelay A_Jump(85,"Idle")
	Idle:
		"####" "#" -1
		stop
	}
}

ACTOR NadeLever
{
	Radius 4
	Height 8
	BounceType Doom
	+THRUACTORS
	+BOUNCEONCEILINGS
	+BOUNCEONFLOORS
	+BOUNCEONWALLS
	+USEBOUNCESTATE
	+FORCEXYBILLBOARD
	Damage (0)
	Friction 0.48
	BounceFactor 0.48
	WallBounceFactor 0.48
	Scale 0.3
	Speed 12
	var int user_frame;
	States
	{
	Spawn:
		LEVR A 0 NoDelay {user_frame = random(0,7);}
	Bounce.Floor:
	Bounce.Ceiling:
	Bounce.Wall:
		TNT1 A 0
		{
			if (++user_frame == 8)
				{user_frame = 0;}
			if (user_frame == 0)
				{return state("SetA");}
			else if (user_frame == 1)
				{return state("SetB");}
			else if (user_frame == 2)
				{return state("SetC");}
			else if (user_frame == 3)
				{return state("SetD");}
			else if (user_frame == 4)
				{return state("SetE");}
			else if (user_frame == 5)
				{return state("SetF");}
			else if (user_frame == 6)
				{return state("SetG");}
			return state("SetH");
		}
	SetA:
		LEVR A 0
		goto Idle
	SetB:
		LEVR B 0
		goto Idle
	SetC:
		LEVR C 0
		goto Idle
	SetD:
		LEVR D 0
		goto Idle
	SetE:
		LEVR E 0
		goto Idle
	SetF:
		LEVR F 0
		goto Idle
	SetG:
		LEVR G 0
		goto Idle
	SetH:
		LEVR H 0
	Idle:
		LEVR "#" 1
		LEVR "#" 0
		{
			if (velz == 0)
				{return state("SetD");}
			return state(0);
		}
		Loop
	Crash:
	Death:
		TNT1 A 0 A_Jump(127,2)
		LEVR D -1
		stop
		LEVR H -1
		stop
	}
}

ACTOR SmokeGrenWep : Weapon
{
	DropItem "SmokeGrenAmmo"
	Weapon.KickBack 10
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 2
	Weapon.AmmoType "SmokeGrenAmmo"
	Weapon.AmmoUse 1
	
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOAUTOFIRE
	+WEAPON.DONTBOB
	+WEAPON.NOALERT
	+WEAPON.NO_AUTO_SWITCH
	+FORCEXYBILLBOARD
	Inventory.PickupSound "misc/pkupitem"
	Inventory.PickupMessage "Picked up a smoke grenade!"
	Tag "Smoke Grenade"
	Obituary "%k must be cheating."
	Scale 0.3
	States
	{
	Spawn:
		SGRN AC 0 NoDelay A_Jump(85,"Idle")
		SGRN G 0
	Idle:
		"####" "#" -1
		stop
	Select:
		TNT1 A 18
		goto Ready
	Deselect:
		TNT1 A 0 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_TakeInventory("FuseTics", 140)
		TNT1 A 0 A_JumpIfNoAmmo(2)
		SMOG B 1 A_WeaponReady
		Loop
		TNT1 A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_GiveInventory("FuseTics",random(1,34))
		SMOG B 10 A_PlaySound("weapons/grenade",0)
		SGRN I 12 A_FireProjectile("NadeLever", frandom(-47.0,-43.0), false, 0, 1, FPF_NOAUTOAIM, frandom(13.0,17.0))
	Hold:
		SGRN I 1 A_GiveInventory("FuseTics", 1)
		TNT1 A 0 A_JumpIfInventory("FuseTics", 139, 2)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_PlaySound("weapons/throw",0)
		TNT1 A 1 A_FireProjectile("SmokeGrenade", frandom(-1.0,1.0), true, 0, 0, FPF_NOAUTOAIM, frandom(-1.0,1.0))
		TNT1 A 50 A_TakeInventory("FuseTics", 140)
		goto Ready
	}
}

ACTOR SmokeGrenade
{
	Radius 4
	Height 8
	BounceType Grenade
	+MISSILE
	-SOLID
	+NOBLOCKMAP
	+THRUACTORS
	+BOUNCEONCEILINGS
	+BOUNCEONFLOORS
	+BOUNCEONWALLS
	+USEBOUNCESTATE
	+FORCEXYBILLBOARD
	Damage (0)
	BounceFactor 0.125
	WallBounceFactor 0.125
	Friction 0.665
	Scale 0.3
	Speed 24
	var int user_tics;
	var int user_frame;
	States
	{
	Spawn:
		SGRN A 0 NoDelay {user_tics = 140-CountInv("FuseTics",AAPTR_TARGET); user_frame = random(0,7);}
		goto Bounce.Wall
	Bounce.Floor:
		TNT1 A 0 A_PlaySound("weapons/nadehit")
	Bounce.Ceiling:
	Bounce.Wall:
		TNT1 A 0 A_AlertMonsters(160.0)
		TNT1 A 0
		{
			if (++user_frame == 8)
				{user_frame = 0;}
			if (user_frame == 0)
				{return state("SetA");}
			else if (user_frame == 1)
				{return state("SetB");}
			else if (user_frame == 2)
				{return state("SetC");}
			else if (user_frame == 3)
				{return state("SetD");}
			else if (user_frame == 4)
				{return state("SetE");}
			else if (user_frame == 5)
				{return state("SetF");}
			else if (user_frame == 6)
				{return state("SetG");}
			return state("SetH");
		}
	SetA:
		SGRN A 0
		goto Idle
	SetB:
		SGRN B 0
		goto Idle
	SetC:
		SGRN C 0
		goto Idle
	SetD:
		SGRN D 0
		goto Idle
	SetE:
		SGRN E 0
		goto Idle
	SetF:
		SGRN F 0
		goto Idle
	SetG:
		SGRN G 0
		goto Idle
	SetH:
		SGRN H 0
	Idle:
		SGRN "#" 1
		SGRN "#" 0
		{
			if (user_tics-- <= 0)
				{return state("Death");}
			else if (velz == 0)
			{
				A_ScaleVelocity(0.8);
				return state("SetG");
			}
			return state(0);
		}
		Loop
	Rest:
		FGRN "#" 1
	Death:
		FGRN "#" 0
		{
			if (user_tics-- <= 0)
				{return state(0);}
			return state("Rest");
		}
		TNT1 A 0 A_SpawnItemEx("SmokeGenerator",0,0,0,velx,vely,velz,0,SXF_ABSOLUTEVELOCITY)
		stop
	}
}

ACTOR SmokeGenerator
{
	Height 1
	Radius 2
	+NOBLOCKMAP
	+MOVEWITHSECTOR
	+FORCEXYBILLBOARD
	Scale 0.3
	States
	{
	Spawn:
		SGRN G 0 {angle = frandom(0,360.0);}
		"####" "#" 0 A_PlaySound("misc/smokeout")
		"####" "#####" 21 {A_SpawnItem("SmokeCloud");}
		//"####" "#####" 14 {A_SpawnItem("SmokeCloud");}
		"####" "#####" 21 {A_SpawnItem("SmokeCloud");}
		//"####" "#####" 14 {A_SpawnItem("SmokeCloud");}
		"####" "#####" 21 {A_SpawnItem("SmokeCloud");}
		"####" "#####" 21 {A_SpawnItem("SmokeCloud");}
		"####" "#####" 21 {A_SpawnItem("SmokeCloud");}
		"####" "#####" 21 {A_SpawnItem("SmokeCloud");}
		"####" "#" -1
		stop
	}
}

ACTOR SmokeCloud
{
	Height 1
	Radius 2
	Speed 0
	+MISSILE
	-SOLID
	+NOBLOCKMAP
	+THRUACTORS
	+NOGRAVITY
	+NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+NOTELEPORT
	+WINDTHRUST
	Alpha 0.5
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			A_SetScale(frandom(0.001,0.01));
			A_ChangeVelocity(frandompick(0,frandom(-0.15,0.15))+cos(angle)*0.1,frandompick(0,frandom(-0.15,0.15))+sin(angle)*0.1,frandom(0,0.15));
		}
		SMOC ABC 0 A_Jump(63,"Idle")
		SMOC D 0
		goto Idle
	Idle:
		SMOC "#" 1
		TNT1 "#" 0
		{
			A_ChangeVelocity(velx*frandom(0.997,1.001),vely*frandom(0.997,1.001),velz*frandom(0.997,1.001),CVF_REPLACE);
			A_SetScale((scalex*249+1.5)/250.0);
			A_FadeOut(0.0005,FTF_REMOVE|FTF_CLAMP);
			if (alpha > 0.15)
				{A_RadiusGive("SmokeInvis",scalex*42.665,RGF_PLAYERS|RGF_MONSTERS);}
		}
		Loop
	}
}

ACTOR SmokeInvis : PowerupGiver
{
	Inventory.PickupSound ""
	Inventory.PickupMessage ""
	Inventory.MaxAmount 0
	Powerup.Duration 27
	Powerup.Type Invisibility
	Powerup.Strength 20
	Powerup.Mode Translucent
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		TNT1 A 18
		Stop
	}
}

ACTOR FragSmoke
{
	Height 1
	Radius 2
	Speed 0
	+MISSILE
	-SOLID
	+NOBLOCKMAP
	+THRUACTORS
	+NOGRAVITY
	+NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+NOTELEPORT
	+WINDTHRUST
	Alpha 0.125
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.001,0.1))
		TNT1 A 0 A_Jump(255,1,2,3,4)
		SMOC A 0
		goto Idle
		SMOC B 0
		goto Idle
		SMOC C 0
		goto Idle
		SMOC D 0
		goto Idle
	Idle:
		SMOC "#" 1
		TNT1 "#" 0
		{
			A_ChangeVelocity(frandom(-0.001,0.001),frandom(-0.001,0.001),frandom(0.001,0.003));
			A_SetScale((scalex*249+1.5)/250.0);
			A_FadeOut(0.001,FTF_REMOVE|FTF_CLAMP);
		}
		Loop
	}
}

ACTOR FragGrenWep : Weapon
{
	DropItem "FragGrenAmmo"
	Weapon.KickBack 10
	Weapon.SlotNumber 8
	Weapon.SelectionOrder 2
	Weapon.AmmoType "FragGrenAmmo"
	Weapon.AmmoUse 1
	
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOAUTOFIRE
	+WEAPON.DONTBOB
	+WEAPON.NOALERT
	+WEAPON.NO_AUTO_SWITCH
	+FORCEXYBILLBOARD
	Inventory.PickupSound "misc/pkupitem"
	Inventory.PickupMessage "Picked up a frag grenade!"
	Tag "Frag Grenade"
	Scale 0.3
	States
	{
	Spawn:
		FGRN AC 0 NoDelay A_Jump(85,"Idle")
		FGRN G 0
	Idle:
		"####" "#" -1
		stop
	Select:
		TNT1 A 18
		goto Ready
	Deselect:
		TNT1 A 0 A_Lower
		Loop
	Ready:
		TNT1 A 0 A_TakeInventory("FuseTics", 140)
		TNT1 A 0 A_JumpIfNoAmmo(2)
		GREN B 1 A_WeaponReady
		Loop
		TNT1 A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_GiveInventory("FuseTics",random(1,34))
		GREN B 10 A_PlaySound("weapons/grenade",0)
		FGRN I 12 A_FireProjectile("NadeLever", frandom(-47.0,-43.0), false, 0, 1, FPF_NOAUTOAIM, frandom(13.0,17.0))
	Cook:
		FGRN I 1 A_GiveInventory("FuseTics", 1)
		TNT1 A 0 A_JumpIfInventory("FuseTics", 139, 2)
		TNT1 A 0 A_ReFire("Cook")
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_PlaySound("weapons/throw",0)
		TNT1 A 1 A_FireProjectile("FragGrenade", frandom(-1.0,1.0), true, 0, 0, FPF_NOAUTOAIM, frandom(-1.0,1.0))
		TNT1 A 50 A_TakeInventory("FuseTics", 140)
		goto Ready
	}
}

ACTOR FragGrenade
{
	Radius 4
	Height 8
	BounceType Grenade
	+MISSILE
	-SOLID
	+NOBLOCKMAP
	+THRUACTORS
	+BOUNCEONCEILINGS
	+BOUNCEONFLOORS
	+BOUNCEONWALLS
	+USEBOUNCESTATE
	+NOEXTREMEDEATH
	+FORCEXYBILLBOARD
	Decal "BulletChip"
	Damage (0)
	BounceFactor 0.125
	WallBounceFactor 0.125
	Friction 0.665
	Scale 0.3
	Speed 24
	var int user_tics;
	var int user_frame;
	Obituary "%o found %k's missing frag grenade."
	States
	{
	Spawn:
		FGRN A 0 NoDelay {user_tics = 140-CountInv("FuseTics",AAPTR_TARGET); user_frame = random(0,7);}
		goto Bounce.Wall
	Bounce.Floor:
		TNT1 A 0 A_PlaySound("weapons/nadehit")
	Bounce.Ceiling:
	Bounce.Wall:
		TNT1 A 0 A_AlertMonsters(160.0)
		TNT1 A 0
		{
			if (++user_frame == 8)
				{user_frame = 0;}
			if (user_frame == 0)
				{return state("SetA");}
			else if (user_frame == 1)
				{return state("SetB");}
			else if (user_frame == 2)
				{return state("SetC");}
			else if (user_frame == 3)
				{return state("SetD");}
			else if (user_frame == 4)
				{return state("SetE");}
			else if (user_frame == 5)
				{return state("SetF");}
			else if (user_frame == 6)
				{return state("SetG");}
			return state("SetH");
		}
	SetA:
		FGRN A 0
		goto Idle
	SetB:
		FGRN B 0
		goto Idle
	SetC:
		FGRN C 0
		goto Idle
	SetD:
		FGRN D 0
		goto Idle
	SetE:
		FGRN E 0
		goto Idle
	SetF:
		FGRN F 0
		goto Idle
	SetG:
		FGRN G 0
		goto Idle
	SetH:
		FGRN H 0
	Idle:
		FGRN "#" 1
		FGRN "#" 0
		{
			if (user_tics-- <= 0)
				{return state("Death");}
			else if (velz == 0)
			{
				A_ScaleVelocity(0.8);
				return state("SetG");
			}
			return state(0);
		}
		Loop
	Death:
		TNT1 A 0 A_PlaySound("weapons/frag",0,1.0,false,0.5)
		TNT1 AAAAA 0 A_SpawnItemEx("FragSmoke",frandom(-16.0,16.0),frandom(-16.0,16.0),0,frandom(-0.5,0.5),frandom(-0.5,0.5),0)
		TNT1 A 0 A_SpawnItemEx("Fragments",0,0,2.0,velx,vely,velz,0,SXF_TRANSFERPOINTERS)
		stop
	}
}

ACTOR Fragments
{
	Radius 1
	Height 2
	+NOBLOCKMAP
	+NOGRAVITY
	Decal "BulletChip"
	Obituary "%o was fragged by %k's grenade."
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {angle=frandom(0,360.0);pitch=frandom(0,-18.0);}
	Continue:
		TNT1 AAAAAAAAAAAAAAAAAAAA 0
		{
			A_CustomBulletAttack(0.0,0.0,1,random(100,199),"RSInvisPuff",160,CBAF_EXPLICITANGLE|CBAF_AIMFACING|CBAF_NORANDOM);
			A_CustomBulletAttack(0.0,0.0,1,random(50,100),"FragPuff",480,CBAF_EXPLICITANGLE|CBAF_AIMFACING|CBAF_NORANDOM);
			angle += 18.0;
		}
		TNT1 A 1
		TNT1 A 0
		{
			angle += 9.0;
			pitch -= 18.0;
			if (pitch < -90.0)
				{return state(0);}
			return state("Continue");
		}
		TNT1 A 1 A_CustomBulletAttack(180.0,180.0,20,random(25,50),"RSInvisPuff",1440,CBAF_AIMFACING|CBAF_NORANDOM)
		TNT1 A 1 A_CustomBulletAttack(180.0,180.0,20,random(1,25),"RSInvisPuff",4320,CBAF_AIMFACING|CBAF_NORANDOM)
		TNT1 A 11
		TNT1 A 0 A_AlertMonsters
		stop
	}
}