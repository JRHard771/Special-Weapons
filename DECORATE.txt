ACTOR R6_Operative : DoomPlayer
{
	Mass 333
	Player.DisplayName "Operative"
	Player.ForwardMove 0.5, 1.0
	Player.SideMove 0.35, 0.7
	Player.MugShotMaxHealth 125
	Player.AttackZOffset 12
	Player.StartItem "R6_Loadout"
	Player.StartItem "R6_Unarmed"
	Player.StartItem "SmokeGrenWep"
	Player.StartItem "FragGrenWep"
	Player.StartItem "FragGrenAmmo", 1
	Player.StartItem "SmokeGrenAmmo", 1
	Player.StartItem "R6_LightArmor"
	Player.StartItem "R6_Stamina", 500
}

ACTOR R6_Stamina : Inventory { Inventory.MaxAmount 500 }
ACTOR sprinting : Inventory { Inventory.MaxAmount 1 }
ACTOR inacc : Inventory { Inventory.MaxAmount 1000 }
ACTOR zoomed : Inventory { Inventory.MaxAmount 1 }

ACTOR primaryWeapons : Inventory { Inventory.MaxAmount 5 }
ACTOR heavyWeapons : Inventory { Inventory.MaxAmount 1 }
ACTOR specialWeapons : Inventory { Inventory.MaxAmount 1 }

ACTOR nothing
{
	-SOLID
	Radius 1
	Height 2
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	}
}

ACTOR RSWeapon : Weapon
{
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse 0
	Weapon.Kickback 0
	Obituary "%o resigned %k to KIA status."
}

ACTOR R6_LightArmor : BasicArmorPickup replaces GreenArmor
{
	Inventory.PickupMessage "Picked up Light Armor!"
	DamageFactor "SmallCal", 0.5
	DamageFactor "LargeCal", 0.9
	Armor.SaveAmount 100
	Armor.SavePercent 10
	Armor.MaxFullAbsorb 10
	Scale 0.335
	States
	{
	Spawn:
		ARM1 A -1
		wait
	}
}

ACTOR R6_MediumArmor : BasicArmorPickup replaces BlueArmor
{
	Inventory.PickupMessage "Picked up Medium Armor!"
	DamageFactor "SmallCal", 0.3
	DamageFactor "LargeCal", 0.7
	Armor.SaveAmount 150
	Armor.SavePercent 15
	Armor.MaxFullAbsorb 15
	Scale 0.335
	States
	{
	Spawn:
		ARM2 A -1
		wait
	}
}

ACTOR R6_HeavyArmor : BasicArmorPickup replaces BlurSphere
{
	Inventory.PickupMessage "Picked up Heavy Armor!"
	DamageFactor "SmallCal", 0.1
	DamageFactor "LargeCal", 0.5
	Armor.SaveAmount 200
	Armor.SavePercent 20
	Armor.MaxFullAbsorb 20
	Scale 0.335
	States
	{
	Spawn:
		ARM3 A -1
		wait
	}
}

const int farcry = 1;
const int weight = 2;
// Recoil for 1 frame firing, 7 frames ready
const float recoil1a = 0.0000313;
const float recoil7b = 0.000067;
// Recoil for 2 frame firing, 6 frames ready
const float recoil2a = 0.0000313;
const float recoil6b = 0.0000729;
// Recoil for 3 frame firing, 5 frames ready
const float recoil3a = 0.0000313;
const float recoil5b = 0.0000813;
// Recoil for 4 frames firing, 4 frames ready
const float recoil4a = 0.0000313;
const float recoil4b = 0.0000938;
// Recoil for 5 frames firing, 3 frames ready
const float recoil5a = 0.0000313;
const float recoil3b = 0.0001146;

#include "Decorate/Ammo.txt"
#include "Decorate/Pistols.txt"
#include "Decorate/SMGs.txt"
#include "Decorate/Shotguns.txt"
#include "Decorate/ARs.txt"
#include "Decorate/Snipers.txt"
#include "Decorate/LMGs.txt"
#include "Decorate/Gear.txt"
#include "Decorate/Monsters.txt"
//Uncomment the following line for more classic Doom zombies
//#include "Decorate/ClassicZeds"
//Uncomment the following line for more lethally equipped zombies
#include "Decorate/Zombies.txt"

ACTOR R6_Loadout : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("LoadoutScript", GetCVar("r6_primary"), GetCVar("r6_secondary"))
		stop
	}
}

ACTOR R6_Unarmed : Weapon replaces Fist
{
	Weapon.SlotNumber 1
	Weapon.SelectionOrder 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoUse 0
	Weapon.Kickback 50
	Tag "CQC"
	Obituary "%k didn't need a gun to stop %o."
	+INVENTORY.UNDROPPABLE
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOALERT
	+WEAPON.MELEEWEAPON
	States
	{
	Select:
		TNT1 A 0 A_Raise
		loop
	Deselect:
		TNT1 A 0 A_Lower
		loop
	Ready:
		TNT1 A 1 {A_WeaponReady; A_TakeInventory("inacc",100);}
		loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,"FireZerk")
		TNT1 A 0 A_Jump(255, 1, 3, 5)
		TNT1 A 6 A_PlaySound("weapons/cqcatt", 0, 0.5, false, 2.0)
		TNT1 A 6 A_CustomPunch(random(5,10),true,CPF_NOTURN,"RSUnarmedPuff")
		goto Ready
		TNT1 A 9 A_PlaySound("weapons/cqcatt", 0, 0.75, false, 2.0)
		TNT1 A 9 A_CustomPunch(random(10,20),true,CPF_NOTURN,"RSUnarmedPuff",68)
		goto Ready
		TNT1 A 12 A_PlaySound("weapons/cqcatt", 0, 1.0, false, 2.0)
		TNT1 A 12 A_CustomPunch(random(20,40),true,CPF_NOTURN,"RSUnarmedPuff",72)
		goto Ready
	FireZerk:
		TNT1 A 0 A_Jump(255, 1, 3, 5)
		TNT1 A 5 A_PlaySound("weapons/cqcatt", 0, 0.7, false)
		TNT1 A 5 A_CustomPunch(random(10,20),true,CPF_NOTURN,"RSUnarmedPuff")
		goto Ready
		TNT1 A 7 A_PlaySound("weapons/cqcatt", 0, 0.85, false)
		TNT1 A 7 A_CustomPunch(random(20,40),true,CPF_NOTURN,"RSUnarmedPuff",68)
		goto Ready
		TNT1 A 9 A_PlaySound("weapons/cqcatt", 0, 1.0, false)
		TNT1 A 9 A_CustomPunch(random(40,80),true,CPF_NOTURN,"RSUnarmedPuff",72)
		goto Ready
	}
}

DamageType SmallCal {}
DamageType LargeCal {}
DamageType Slime
{
	NoArmor
}

ACTOR RSBulletDebris
{
	Radius 4
	Height 4
	Speed 2
	+MISSILE
	+NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+NOTELEPORT
	+FLOORCLIP
	+WINDTHRUST
	+NOBLOCKMAP
	+THRUACTORS
	+MOVEWITHSECTOR
	+BOUNCEONWALLS
	+BOUNCEONCEILINGS
	Damage (0)
	Scale 0.1
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {A_SetScale(frandom(0.025,0.1));A_SetGravity(scalex*10);A_SetScale(scalex*randompick(-1,1),scalex);}
		TNT1 A 0 A_Jump(255,1,2,3)
		DBRS A 0
		goto Idle
		DBRS B 0
		goto Idle
		DBRS C 0
		goto Idle
	Idle:
		DBRS "##########" 4
	Death:
	Crash:
		DBRS "#" random(18,35)
		goto Fade
	Fade:
		DBRS "#" 1 A_FadeOut(0.01,FTF_REMOVE|FTF_CLAMP)
		loop
	}
}

ACTOR RSInvisPuff : BulletPuff
{
	+BLOODLESSIMPACT
	+NOEXTREMEDEATH
	ProjectileKickback 25
	DamageType LargeCal
	AttackSound "misc/bullet"
	States
	{
	Spawn:
	Melee:
	Crash:
		TNT1 A 0 NoDelay A_AlertMonsters(192.0)
		stop
	}
}

ACTOR RSPuff : BulletPuff replaces BulletPuff
{
	Height 1
	DamageType LargeCal
	ProjectileKickback 25
	-SOLID
	+NOBLOCKMAP
	+NOEXTREMEDEATH
	+PUFFGETSOWNER
	+FORCEYBILLBOARD
	RenderStyle Translucent
	AttackSound "misc/bullet"
	Alpha 0.1125
	XScale 0.1
	YScale 0.2
	VSpeed 0
	States
	{
	Spawn:
	Melee:
	Crash:
		TNT1 A 0 NoDelay
		{
			A_AlertMonsters(192.0);
			A_SetScale(frandom(0.0025,0.025),frandom(0.005,0.05));
			A_ChangeVelocity(frandom(-0.1,0.1),frandom(-0.1,0.1),frandompick(-1.0,-0.5,-0.25,-0.125,0.0));
		}
		TNT1 AAAAA 0 A_SpawnItemEx("RSBulletDebris",0,0,0,frandom(-8.0,8.0),frandom(-8.0,8.0),frandom(-8.0,8.0))
		TNT1 A 0 A_Jump(255,1,2,3,4)
		SMOC A 0
		goto Idle
		SMOC B 0
		goto Idle
		SMOC C 0
		goto Idle
		SMOC D 0
		goto Idle
	Idle:
		SMOC "#" 1
		{
			A_SetScale(scalex+0.01,scaley+0.005-0.015*velz);
			A_FadeOut(0.001,FTF_REMOVE|FTF_CLAMP);
		}
		Loop
	}
}

ACTOR FragPuff : RSPuff
{
	ProjectileKickback 25
	Alpha 0.0575
	States
	{
	Spawn:
	Melee:
	Crash:
		TNT1 A 0 NoDelay
		{
			A_AlertMonsters(192.0);
			A_SetScale(frandom(0.00125,0.0125),frandom(0.005,0.05));
			A_ChangeVelocity(frandom(-0.1,0.1),frandom(-0.1,0.1),frandompick(-1.0,-0.5,-0.25,-0.125,0.0));
		}
		TNT1 A 0 A_SpawnItemEx("RSBulletDebris",0,0,0,frandom(-8.0,8.0),frandom(-8.0,8.0),frandom(-8.0,8.0))
		TNT1 A 0 A_Jump(255,1,2,3,4)
		SMOC A 0
		goto Idle
		SMOC B 0
		goto Idle
		SMOC C 0
		goto Idle
		SMOC D 0
		goto Idle
	Idle:
		SMOC "#" 1
		{
			A_SetScale(scalex+0.01,scaley+0.005-0.015*velz);
			A_FadeOut(0.001,FTF_REMOVE|FTF_CLAMP);
		}
		Loop
	}
}

ACTOR RSPuffSm : RSPuff
{
	DamageType SmallCal
	ProjectileKickback 25
}

ACTOR RSUnarmedPuff : RSPuff
{
	+FORCEPAIN
	+BLOODLESSIMPACT
	+INVISIBLE
	States
	{
	Spawn:
	Melee:
	Crash:
		TNT1 A 0 NoDelay A_PlaySound("weapons/cqchit")
		stop
	}
}

ACTOR shotStopper
{
	-SOLID
	+SHOOTABLE
	Mass 99999
	+NODAMAGE
	Radius 16
	Height 50
	States
	{
	Spawn:
		TNT1 A 4
		stop
	}
}